plugins {
    id "java"
    id 'org.photonvision.tools.WpilibTools' version '2.3.1-photon'
    id "edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin" version "2020.2"
    id "com.diffplug.spotless" version "8.1.0"
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = "https://maven.photonvision.org/repository/internal/" }
    }
    wpilibRepositories.addAllReleaseRepositories(it)
    wpilibRepositories.addAllDevelopmentRepositories(it)
}

// Configure the version number.
apply from: "versioningHelper.gradle"

ext {
    pubVersion = versionString
    isDev = pubVersion.startsWith("dev")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

def nativeName = wpilibTools.platformMapper.platformPath
ext.outputsFolder = file("$buildDir/outputs")

println("Building for $nativeName")

tasks.register('copyNativeLibrary', Sync) {
    from "${projectDir}/cmake_build"
    into "${outputsFolder}/${nativeName}/shared"
    include "libphotonlibcamera.so"

    // And flatten, since windows is stupid
    eachFile {
      path = name
    }
    includeEmptyDirs = false

    build.dependsOn it
    publish.dependsOn it
}

apply from: "publish.gradle"
